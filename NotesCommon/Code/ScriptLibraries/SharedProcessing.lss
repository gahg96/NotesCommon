'++LotusScript Development Environment:2:5:(Options):0:74
%REM
	Library Processing
	Created Feb 11, 2016 by Mariusz Jakubowski
	Maintains a queue of documents to process by agents.
%END REM
Option Public
Option Declare
Use "SharedLogging"
Use "SharedBase"
Use "SharedCollections"


'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Public Class WorkQueue
Declare Sub Initialize
Declare Private Function GetDoc(operation As String, unid As String) As NotesViewEntry
Declare Public Sub AddToProcess(operation As String, unid As String)
Declare Public Sub Processed(operation As String, unid As String)

'++LotusScript Development Environment:2:5:(Declarations):0:10

Private ToProcess As NotesView

%REM
	A queue of documents to process.
%END REM
Public Class WorkQueue
    unids As SimpleList
    idx As Integer
    
    %REM
	   Initializes a queue of documents to process.
    %END REM
    Public Sub New(operation As String)
        Set unids = New SimpleList()
        idx = 0
        Dim ec As NotesViewEntryCollection
        Set ec = ToProcess.GetAllEntriesByKey(operation, true)
        ToProcess.AutoUpdate = False
        Dim e As NotesViewEntry
        Set e = ec.GetFirstEntry()
        While Not e Is Nothing 
            Call unids.Add(e.UniversalID)
            Set e = ec.GetNextEntry(e)
        Wend
        ToProcess.AutoUpdate = True
    End Sub
    
    %REM
    	Returns unid of next document to process or empty string if all 
        documents were processed.
    %END REM
    Public Function GetNext() As String
        If idx < unids.Count Then
            GetNext = unids.GetItem(idx)
        Else
            GetNext = ""
        End If
        idx = idx + 1
    End Function
    
End Class

'++LotusScript Development Environment:2:2:Initialize:1:10
Sub Initialize
    Set ToProcess = CurrentDatabase.GetView("DocumentsToProcess")
End Sub



'++LotusScript Development Environment:2:1:GetDoc:4:8
%REM
    Finds a document for processing in a queue.
%END REM
Private Function GetDoc(operation As String, unid As String) As NotesViewEntry
    Dim key(1) As String
    key(0) = operation
    key(1) = unid
    Set GetDoc = ToProcess.GetEntryByKey(key, True)
End Function



'++LotusScript Development Environment:2:2:AddToProcess:4:8
%REM
    Adds a document to processing queue.
%END REM
Public Sub AddToProcess(operation As String, unid As String)
    If GetDoc(operation, unid) Is Nothing Then
        Dim doc As New NotesDocument(CurrentDatabase)
        doc.Form = "DocumentToProcess"
        doc.operation = operation
        doc.unid = unid
        Call doc.Save(True, False)
    End If
End Sub



'++LotusScript Development Environment:2:2:Processed:4:8
%REM
    Removes a document from a queue of documents to process.
%END REM
Public Sub Processed(operation As String, unid As String)
    Dim e As NotesViewEntry
    Set e = GetDoc(operation, unid)
    If Not e Is Nothing Then
        Call e.Document.RemovePermanently(True)
    End If
End Sub



