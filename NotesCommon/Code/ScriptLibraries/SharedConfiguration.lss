'++LotusScript Development Environment:2:5:(Options):0:74
%REM
	Library SharedConfiguration
	Created Jul 22, 2014 by Mariusz Jakubowski
	Description: Utilities for managing configuration.
%END REM
Option Public
Option Declare
Use "SharedBase"
Use "SharedLogging"
Use "SharedNative"


'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Public Sub CopyToClipboard()
Declare Public Sub PasteFromClipboard()
Declare Public Sub UpdateLastSave()

'++LotusScript Development Environment:2:5:(Declarations):0:10

'++LotusScript Development Environment:2:2:CopyToClipboard:8:8
%REM
    Sub CopyToClipboard
    Description: Copy document contents to clipboard.
    Each field is saved in text format, each field is delimited by new line.
    The format for each line is:
    ItemName\: ItemValue (without \ character)
%END REM
Public Sub CopyToClipboard()
    On Error GoTo err1
    Call InitUI()
    Dim txt As String
    ForAll item In document.Items
        Dim ite As NotesItem
        If Left$(item.Name, 1) <> "$" Then
            txt = txt + item.Name + ":" + item.Text + Chr$(13)
        End If
    End ForAll
    Call setClipboardText(txt)
    Exit Sub
err1:
    Call ShowErrorMessage("")
    Exit Sub
End Sub



'++LotusScript Development Environment:2:2:PasteFromClipboard:6:8
%REM
    Sub PasteFromClipboard
    Description: Paste field values from clipboard into document.
    See CopyToClipboard for description of a data format.
%END REM
Public Sub PasteFromClipboard()
    On Error GoTo err1
    Call InitUI()
    Dim errDebug As String
    Dim txt As String
    Call getClipBoardText(False, txt)
    Dim itemArr As Variant
    itemArr = Split(txt, Chr$(13))
    ForAll itemTxt In itemArr
        If itemTxt = "" Then Exit ForAll            
        errDebug = itemTxt 
        Dim idx As Integer
        idx = InStr(itemTxt, ":")
        If idx = 0 Then Exit ForAll
        Dim itemName As String
        Dim value As String
        itemName = Left$(itemTxt, idx-1)
        value = Mid$(itemTxt, idx+1)
        Call document.ReplaceItemValue(itemName, value)
    End ForAll
    Call uidoc.Reload()
    Exit Sub
err1:
    Call ShowErrorMessage(errDebug)
    Exit Sub
End Sub



'++LotusScript Development Environment:2:2:UpdateLastSave:5:8
%REM
    Sub UpdateLastSave
    Description: Updates information about last save.
%END REM
Public Sub UpdateLastSave()
    Call InitUI()   
    document.LastSavedDate = Now()
    document.LastSavedAuthor = Session.CommonUserName
End Sub

